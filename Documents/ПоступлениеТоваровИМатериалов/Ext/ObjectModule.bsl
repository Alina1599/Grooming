#Область ОбработчикиСобытий
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаДокумента = Номенклатура.Итог("Сумма");
КонецПроцедуры
 
Процедура ОбработкаПроведения(Отказ, Режим)         
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.УчетнаяПолитика = Перечисления.ВидыУчетнойПолитики.FEFO Тогда
		ОтражатьСрокиГодности = Истина;
	Иначе
		ОтражатьСрокиГодности = Ложь;
	КонецЕсли;                            
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =  "ВЫБРАТЬ
	|	ПоступлениеТоваровИМатериаловНоменклатура.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровИМатериаловНоменклатура.Цена КАК Цена,
	|	СУММА(ПоступлениеТоваровИМатериаловНоменклатура.Количество) КАК Количество,
	|	СУММА(ПоступлениеТоваровИМатериаловНоменклатура.Сумма) КАК Сумма,
	|	ПоступлениеТоваровИМатериаловНоменклатура.СрокГодности КАК СрокГодности,
	|	ПоступлениеТоваровИМатериаловНоменклатура.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.ПоступлениеТоваровИМатериалов.Номенклатура КАК ПоступлениеТоваровИМатериаловНоменклатура
	|ГДЕ
	|	ПоступлениеТоваровИМатериаловНоменклатура.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровИМатериаловНоменклатура.Номенклатура,
	|	ПоступлениеТоваровИМатериаловНоменклатура.Цена,
	|	ПоступлениеТоваровИМатериаловНоменклатура.СрокГодности,
	|	ПоступлениеТоваровИМатериаловНоменклатура.Номенклатура.СчетБухгалтерскогоУчета";     
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать();
	Пока ВыборкаИтоги.Следующий() Цикл       
		
		Движение = Движения.ТоварыНаСкладах.Добавить(); 
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаИтоги.Номенклатура;
		Движение.Склад = Склад;   
		Движение.Количество = ВыборкаИтоги.Количество;
		Движение.Сумма = ВыборкаИтоги.Сумма;     
		
		Если ОтражатьСрокиГодности Тогда 
			Движение.СрокГодности = ВыборкаИтоги.СрокГодности;
		КонецЕсли;   
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = ВыборкаИтоги.СчетБухгалтерскогоУчета;
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаИтоги.Сумма;
		Движение.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = ВыборкаИтоги.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Поставщик;
	КонецЦикла;       
КонецПроцедуры
#КонецОбласти
                                                                                              
